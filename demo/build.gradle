plugins {
    id 'application'
    id 'org.beryx.jlink' version '2.23.3'
}

repositories {
    mavenCentral()
    jcenter()

    flatDir {
        dirs "${project(':demo').projectDir}/libs"
    }
}

dependencies {
    testImplementation "org.testfx:testfx-core:4.0.16-alpha"
    testImplementation "junit:junit:4.13.1"
    testImplementation "org.testfx:testfx-junit:4.0.16-alpha"

    implementation name: 'scenicview'
    implementation "fr.brouillard.oss:cssfx:11.4.0"
    implementation 'org.kordamp.ikonli:ikonli-core:12.2.0'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.2.0'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome5-pack:12.2.0'
    implementation project(':materialfx')
}

application {
    String main = project.findProperty("chooseMain").toString()
    if (main != "null" && !main.trim().isEmpty()) {
        getMainClass().set(moduleName.concat('/').concat(main))
    } else {
        getMainClass().set("io.github.palexdev.materialfx.demo.Demo")
    }
}

jlink {
    imageZip = file("$buildDir/$rootProject.name-$rootProject.version" + '.zip')
    launcher {
        mainClass = "io.github.palexdev.materialfx.demo.Demo"
        name = 'MaterialFX Demo'
    }
    jpackage {
        imageOptions = ['--icon', 'src/main/resources/logo.ico']
    }
}